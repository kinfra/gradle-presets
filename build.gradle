plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version '0.10.1'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

group = "ru.kontur.jinfra"
version = "1.0.1"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin"
}

repositories {
    jcenter()
}

gradlePlugin {
    plugins {
        presets {
            id = "ru.kontur.jinfra.presets"
            implementationClass = "ru.kontur.jinfra.gradle.presets.JinfraPresetsPlugin"
        }
    }
}

pluginBundle {
    description = "Default configuration for Gradle projects used in Kontur"
}

task install(group: 'publishing') {
    dependsOn publishToMavenLocal
}

afterEvaluate {
    publishing {
        publications {
            pluginMaven {
                artifact publishPluginJar
            }
        }

        repositories {
            def properties = project.properties
            def repoUrl = properties["publishing.repo.url"]
            if (repoUrl) {
                maven {
                    url = repoUrl

                    credentials {
                        username = properties["publishing.repo.username"]
                        password = properties["publishing.repo.password"]
                    }
                }
            }
        }
    }
}
