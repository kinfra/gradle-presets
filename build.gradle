plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version '0.12.0'
    // Kotlin version must match the version provided by Gradle
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group = "ru.kontur.kinfra"
version = "1.4"

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin"
}

repositories {
    jcenter()
}

gradlePlugin {
    plugins {
        presets {
            id = "ru.kontur.kinfra.presets"
            displayName = "Kinfra presets for Gradle"
            implementationClass = "ru.kontur.kinfra.gradle.presets.KinfraPresetsPlugin"
        }
    }
}

pluginBundle {
    description = "Default configuration for Gradle projects used in Kontur"
    vcsUrl = "https://github.com/kinfra/gradle-presets"
    website = "https://github.com/kinfra/gradle-presets"
    tags = ["defaults"]
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

task install(group: 'publishing') {
    dependsOn publishToMavenLocal
}

afterEvaluate {
    publishing {
        publications {
            pluginMaven {
                artifact publishPluginJar
            }
        }

        repositories {
            def properties = project.properties
            def repoUrl = properties["publishing.repo.url"]
            if (repoUrl) {
                maven {
                    url = repoUrl

                    credentials {
                        username = properties["publishing.repo.username"]
                        password = properties["publishing.repo.password"]
                    }
                }
            }
        }
    }
}
