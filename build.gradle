plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version '0.10.1'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
}

group = "ru.kontur.jinfra"
version = "1.0-RC1"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin"
}

repositories {
    jcenter()
}

gradlePlugin {
    plugins {
        presets {
            id = "ru.kontur.jinfra.presets"
            implementationClass = "ru.kontur.jinfra.gradle.presets.PresetsPlugin"
        }
    }
}

pluginBundle {
    description = "Default configuration for Gradle projects used in Kontur"
}

task install(group: 'publishing') {
    dependsOn publishToMavenLocal
}

task sourcesJar(type: Jar) {
    group = "build"
    description = "Assemble a JAR containing all sources of the module."
    archiveClassifier = "sources"

    from sourceSets.main.allSource
    dependsOn classes
}

assemble.dependsOn sourcesJar

afterEvaluate {
    publishing {
        publications {
            getByName("pluginMaven") {
                artifact sourcesJar
            }
        }
    }
}
